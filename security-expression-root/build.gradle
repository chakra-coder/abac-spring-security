import java.time.ZoneId
import java.time.ZonedDateTime
import java.time.format.DateTimeFormatter

buildscript {
    ext {
        springBootVersion = '2.0.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'application'
    id 'java'
    id 'maven'
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'maven-publish'

dependencies {
    compile project(":xacml-resource-models")

    compile('org.springframework.boot:spring-boot-starter')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-security')

    compileOnly "org.springframework.boot:spring-boot-configuration-processor"

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

compileJava.dependsOn(processResources)

jar{
    classifier = 'default'
}

mainClassName = 'com.github.joffryferrater.pep.PepApplication'
version = "0.2.1"

shadowJar {
    classifier = null
    baseName = 'abac-spring-security'
    archiveName = "${baseName}-${version}.jar"
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Implementation-Title': rootProject.name
        attributes 'Implementation-Version': rootProject.version
        attributes 'Implementation-Vendor-Id': rootProject.group
        attributes 'Build-Time': ZonedDateTime.now(ZoneId.of("UTC"))
                .format(DateTimeFormatter.ISO_ZONED_DATE_TIME)
        attributes 'Built-By': InetAddress.localHost.hostName
        attributes 'Created-By': 'Gradle ' + gradle.gradleVersion
        attributes 'Main-Class': mainClassName
    }
}

publishing {
    publications {
        jar(MavenPublication) {
            groupId 'com.github.joffryferrater'
            artifactId 'abac-spring-security'
            artifact shadowJar
        }
    }
}

artifactoryPublish {
    publications ('jar')
}

publish.finalizedBy 'artifactoryPublish'